import org.gradle.plugins.ide.eclipse.model.*

buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
		classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
		classpath 'me.tatarka:gradle-retrolambda:3.2.4'
	}
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'license'
apply plugin: 'me.tatarka.retrolambda'

sourceCompatibility = 1.8
targetCompatibility = 1.8

license {
	ext.year = 2016
	ext.name = 'Steffen Schaefer'
	header rootProject.file('LICENSE')
}

repositories { mavenCentral() }

dependencies {
	compile gradleApi()
	testCompile group: 'junit', name: 'junit', version: '4.11'
	retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.1.0'
}

retrolambda {
	javaVersion JavaVersion.VERSION_1_6
	if(System.getProperty("os.name").toLowerCase().contains('mac')) {
		if(jdk == null) {
			jdk = '/usr/libexec/java_home -v 1.8'.execute().text
		}
		if(oldJdk == null) {
			oldJdk = '/usr/libexec/java_home -v 1.6'.execute().text
		}
	}
}

tasks.withType(JavaCompile) {
	options.encoding = rootProject.encoding
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
}

task javadocJar (type: Jar, dependsOn: javadoc) {
	from javadoc.destinationDir
}

javadoc {
	options {
		links 'http://www.gradle.org/docs/current/javadoc/', 'http://docs.oracle.com/javase/6/docs/api/'
	}
}

group='de.richsource.gradle.plugins'
version='0.7-SNAPSHOT'
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact javadocJar { classifier = 'javadoc' }
			artifact sourceJar { classifier = 'sources' }
			pom.withXml {
				def root = asNode()
				root.appendNode('packaging', 'jar')
				root.appendNode('name', 'Gradle GWT plugin')
				root.appendNode('description', 'Gradle plugin to support GWT related tasks.')
				root.appendNode('inceptionYear', '2013')
				root.appendNode('url', 'https://github.com/steffenschaefer/gwt-gradle-plugin')
				def license = root.appendNode('licenses').appendNode('license')
				license.appendNode('name', 'Apache License, Version 2.0')
				license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
				license.appendNode('distribution', 'repo')
				def scm = root.appendNode('scm')
				scm.appendNode('url', 'https://github.com/steffenschaefer/gwt-gradle-plugin')
				scm.appendNode('connection', 'scm:git:https://github.com/steffenschaefer/gwt-gradle-plugin.git')
			}
		}
	}
	repositories {
		maven {
			url new File(rootProject.projectDir, 'repo').toURI()
		}
	}
}

rootProject.tasks.publish.dependsOn project.tasks.publish

eclipse {
	classpath {
		file {
			whenMerged {Classpath cp ->
				String gradleHome = gradle.getGradleHomeDir().absolutePath.replace(File.separator, '/')
				String gradleSrc = "${gradleHome}/src"
				cp.entries.each {ClasspathEntry entry ->
					if ((entry in AbstractLibrary) && (entry.library.file.name.startsWith('gradle-'))) {
						entry.sourcePath = new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory().fromPath(gradleSrc)
					}
				}
			}
		}
	}
}
